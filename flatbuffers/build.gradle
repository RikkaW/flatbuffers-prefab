import java.nio.file.Paths

plugins {
    id 'com.android.library'
    id 'com.vanniktech.maven.publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.12.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=none"
            }
        }
    }
    buildFeatures {
        androidResources false
        prefabPublishing true
        buildConfig false
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    prefab {
        flatbuffers {
            headers "src/main/cpp/include"
        }
    }
}

// prefabPublishing does not support header only lib (https://issuetracker.google.com/issues/173348031)

def removeDummyStaticLibraryRelease = task('removeDummyStaticLibraryRelease').doLast {
    def dir = Paths.get(project.buildDir.path, 'intermediates',
            'prefab_package', 'release', 'prefab', "modules")
    def modules = ['flatbuffers']
    for (module in modules) {
        def libs = dir.resolve("${module}/libs")
        println("Remove $libs")
        delete(libs)
        mkdir(libs)
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'prefabReleasePackage') {
        task.finalizedBy removeDummyStaticLibraryRelease
    }
}

ext {
    POM_NAME = "FlatBuffers Prefab"
    POM_DESCRIPTION = "Prefab version of FlatBuffers."
}